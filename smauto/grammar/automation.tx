import entity
import condition
import types
import utils

// === Overall Automation ===
Automation:
    'Automation' name=ID
        (
        ('condition:' condition=Condition)
        ('description:' description=STRING)?
        ('actions:' actions*=Action)?
        ('freq:' freq=INT)?
        ('enabled:' enabled=BOOL)?
        ('continuous:' continuous=BOOL)?
        ('checkOnce:' checkOnce=BOOL)?
        ('delay:' delay=FLOAT)?
        ('starts:' '-' starts*=[Automation:FQN|+pm:automations]['-'])?
        ('stops:' '-' stops*=[Automation:FQN|+pm:automations]['-'])?
        ('after:' '-' after*=[Automation:FQN|+pm:automations]['-'])?
        )#
    'end'
;

AutomationDependency:
    automation=[Automation:FQN|+pm:automations] ('on' exitStatus=BOOL)?
;


// === Actions ===
Action:
    FloatAction | IntAction | BoolAction | StringAction | ListAction | DictAction
;

IntAction:
    ('-' attribute=[IntAttribute:FQN|+pm:entities.attributes] ':' value=INT)
;

FloatAction:
    ('-' attribute=[FloatAttribute:FQN|+pm:entities.attributes] ':' value=STRICTFLOAT)
;

StringAction:
    ('-' attribute=[StringAttribute:FQN|+pm:entities.attributes] ':' value=STRING)
;

BoolAction:
    ('-' attribute=[BoolAttribute:FQN|+pm:entities.attributes] ':' value=BOOL)
;

ListAction:
    ('-' attribute=[ListAttribute:FQN|+pm:entities.attributes] ':' value=List)
;

DictAction:
    ('-' attribute=[DictAttribute:FQN|+pm:entities.attributes] ':' value=Dict)
;
