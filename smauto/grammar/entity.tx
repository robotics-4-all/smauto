import broker

Entity:
    'Entity:'
        (
			('name:' name=ID)?
			('type:' etype=EntityType)
			('topic:' topic=STRING)
			('freq:' freq=NUMBER)?
			('broker:' broker=[Broker])
			('attributes:' '-' attributes*=Attribute['-'])
		)#
;

EntityType:
    'sensor' |
    'actuator' |
    'hybrid'
;

Attribute:
    NumericAttribute    |
    BoolAttribute       |
    StringAttribute     |
    ListAttribute       |
    DictAttribute
;

NumericAttribute:
    IntAttribute | FloatAttribute
;

IntAttribute:
    name=ID ':' 'int' ('=' default=INT)?
        ('->' generator=NumericValueGen ('with noise' noise=Noise)?)?
;

FloatAttribute:
    name=ID ':' 'float' ('=' default=FLOAT)?
        ('->' generator=NumericValueGen ('with noise' noise=Noise)?)?
;

StringAttribute:
    name=ID ':' 'str' ('=' default=STRING)?
;

BoolAttribute:
    name=ID ':' 'bool' ('=' default=BOOL)?
        ('->' generator=BooleanValueGen)?
;

ListAttribute:
    name=ID ':' 'list' ('=' '[' default*=OBJECT[','] ']')?
        ('->' generator=ListValueGen)?
;

DictAttribute:
    name=ID ':' 'dict' ('=' '{' items*=Attribute[','] '}')?
        ('->' generator=DictValueGen)?
;


NumericValueGen:
    ConstantFun     |
    LinearFun       |
    GaussianFun     |
    ReplayFun       |
    ReplayFileFun
;

BooleanValueGen: ReplayFun | ReplayFileFun;

ListValueGen: ReplayFun | ReplayFileFun;

DictValueGen: ReplayFun | ReplayFileFun;

GaussianFun:
    'gaussian' '(' value=NUMBER ',' maxValue=NUMBER ',' sigma=NUMBER ')'
;

ConstantFun:
    'constant' '(' value=NUMBER ')'
;

LinearFun:
    'linear' '(' start=NUMBER ',' step=NUMBER ')'
;

ReplayFun:
    'replay' '(' '[' values*=BASETYPE[','] ']' ',' times=INT ')'
;

ReplayFileFun:
    'replay' '(' filepath=STRING ')'
;


Noise: UniformNoise | GaussianNoise;

UniformNoise:
    'uniform' '(' min=NUMBER ',' max=NUMBER ')'
;

GaussianNoise:
    'gaussian' '(' mu=NUMBER ',' sigma=NUMBER ')'
;


NID: /(.*?)\n/;
